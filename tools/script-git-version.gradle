buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'org.ajoberstar:grgit:2.2.0'
    }
}

import org.ajoberstar.grgit.Grgit

/**
 * git.describe()
 *
 * Find the most recent tag that is reachable from HEAD. If the tag points to the commit,
 * then only the tag is shown. Otherwise, it suffixes the tag name with the number of additional
 * commits on top of the tagged object and the abbreviated object name of the most recent commit.
 *
 * More info: https://git-scm.com/docs/git-describe
 */

ext {
    git = Grgit.open(currentDir: projectDir)

    gitVersionInfo = git.describe(match: ["[0-9]*\\.[0-9]*\\.[0-9]*"]).split("-") as List
    gitVersionName = gitVersionInfo.remove(0)
    gitVersionInfo = gitVersionInfo.join("-")

    gitVersionCode = git.log().size()
    gitVersionCodeTime = git.head().time
    gitBranchName = git.branch.current.name
}

task printVersion() {
    println("Version Name: $gitVersionName")
    println("Version Code: $gitVersionCode")
    println("Version Info: $gitVersionInfo" )
    println("Version Code Time: $gitVersionCodeTime")
    println("Branch name: $gitBranchName")
}